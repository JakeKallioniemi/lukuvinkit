plugins {
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testImplementation 'io.cucumber:cucumber-java:5.5.0'
    testImplementation 'io.cucumber:cucumber-junit:5.5.0'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
}

mainClassName = 'lukuvinkit.Main'

run {
    standardInput = System.in
}

test {
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

jar {
    manifest {
        attributes 'Main-Class': 'lukuvinkit.Main'
    }
}

checkstyle {
    configFile = file("${rootProject.projectDir}/config/checkstyle/google_checks.xml")
    toolVersion = "8.31"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/Main.class'])
        }))
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'lukuvinkit', 'src/test/resources', '--strict']
        }
    }
}
